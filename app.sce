clear;clc

//Callbacks
function calculate()
    set("log_text", "string", "Status: Running (check console for any error if the process stuck)")
    Sheet = readxls(get("xls_input","String"))
    Data=Sheet(3)
    Component=Data(3:$,1)
    Mass_in=Data(3:$,2)
    CpA=Data(3:$,3)
    CpB=Data(3:$,4)
    CpC=Data(3:$,5)
    CpD=Data(3:$,6)
    CpE=Data(3:$,7)
    Tr = evstr(get("Tr_input","String"))
    Tin = evstr(get("Tin_input","String"))
    Tout = evstr(get("Tout_input","String"))
    
    for i=1:size(Component)(1)
        Cpin(i)=CpA(i)*(Tin-Tr)+CpB(i)/2*(Tin^2-Tr^2)+CpC(i)/3*(Tin^3-Tr^3)+CpD(i)/4*(Tin^4-Tr^4)+CpE(i)/5*(Tin^5-Tr^5)
        Cpout(i)=CpA(i)*(Tout-Tr)+CpB(i)/2*(Tout^2-Tr^2)+CpC(i)/3*(Tout^3-Tr^3)+CpD(i)/4*(Tout^4-Tr^4)+CpE(i)/5*(Tout^5-Tr^5)
        dHin(i)=Mass_in(i)*Cpin(i)
        dHout(i)=Mass_in(i)*Cpout(i)
    end

    mprintf('\n-------------------------------------------------------------\n')
    mprintf('Comp No. Input      CpdT in     CpdT out    ΔH in      ΔH out')
    mprintf('\n-------------------------------------------------------------\n')
    disp([[1:size(Component)(1)]' Mass_in Cpin Cpout dHin dHout])
    mprintf('\n-------------------------------------------------------------\n')
    Q = sum(dHout)-sum(dHin)
    mprintf('Heat needed for (unit) to work is %.4f kJ/h',Q)
    set("log_text", "string", "Status: Process done!")
    sleep(3000)
    set("log_text", "string", "Status: Waiting for input")
endfunction


//Create a figure with a gridbag layout
f = figure( ...
"figure_name"     , gettext("Heat Exchanger Simulation"),...
"dockable"        , "off",...
"infobar_visible" , "off",...
"toolbar"         , "none",...
"menubar_visible" , "off",...
"menubar"         , "none",...
"default_axes"    , "off",...
"layout"          , "gridbag",...
"visible"         , "on");

data_dialog = uicontrol(f , ...
"style"               , "frame"                     , ...
"border", createBorder("titled", createBorder("line", "#000"), "Data"),...
"layout", "grid",...
"layout_options", createLayoutOptions("grid", [2,1],[0,10]),...
"constraints", createConstraints("gridbag",[1, 1, 1, 1], [1, 0], "horizontal", "upper", [0, 0]));

Tr_input = uicontrol(data_dialog,...
"tag", "Tr_input",...
"style", "edit",...
"constraints", createConstraints("grid")...
);

Tr_label =uicontrol(data_dialog,...
"style", "text",...
"string", "T reference (K)",...
"constraints", createConstraints("grid")...
);

xls_input =uicontrol(data_dialog,...
"tag", "xls_input",...
"style", "edit",...
"constraints", createConstraints("grid")...
);

xls_label = uicontrol(data_dialog,...
"style", "text",...
"string", "Component data (.xls)",...
"constraints", createConstraints("grid")...
);


io_dialog = uicontrol(f , ...
"style"               , "frame"                     , ...
"border", createBorder("titled", createBorder("line", "#000"), "Input/Output"),...
"layout", "grid",...
"layout_options", createLayoutOptions("grid", [2,1],[0,10]),...
"constraints", createConstraints("gridbag",[1, 2, 1, 1], [1, 0], "horizontal", "upper", [0, 0]));

uicontrol(io_dialog,...
"tag", "Tin_input",...
"style", "edit",...
"constraints", createConstraints("grid")...
);

uicontrol(io_dialog,...
"style", "text",...
"string", "T input (K)",...
"constraints", createConstraints("grid")...
);

uicontrol(io_dialog,...
"tag", "Tout_input",...
"style", "edit",...
"constraints", createConstraints("grid")...
);

uicontrol(io_dialog,...
"style", "text",...
"string", "T output (K)",...
"constraints", createConstraints("grid")...
);

submit_btn = uicontrol(f,...
"style", "pushbutton",...
"string", "run",...
"callback", "calculate",...
"constraints",  createConstraints("gridbag",[1, 3, 1, 1], [1, 0], "horizontal", "upper", [0, 0])...
);

log_text = uicontrol(f,...
"tag", "log_text",...
"style", "text",...
"string", "Status: waiting for input",...
"constraints",  createConstraints("gridbag",[1, 4, 1, 1], [1, 0], "horizontal", "upper", [0, 0])...
);
